apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

final def readAndUnderstoodLicense = false

android {
    defaultConfig {
        applicationId "io.github.domi04151309.powerapp"
        compileSdk 34
        minSdkVersion 23
        //noinspection EditedTargetSdkVersion
        targetSdkVersion 34
        versionCode 190
        versionName "1.9.0"
    }
    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        disable += 'MissingTranslation'
    }
    namespace 'io.github.domi04151309.powerapp'
}

preBuild.doFirst {
    final def data = file('./src/main/res/xml/pref_about.xml').getText().contains('app:key="license"')
    if (!data) throw new Exception(
            'Please note that removing the license from the about page is not allowed if you plan' +
                    'to publish your modified version of this app. ' +
                    'Please read the project\'s LICENSE.'
    )
    if (!(android.defaultConfig.applicationId.contains('domi04151309') || readAndUnderstoodLicense)) {
        throw new Exception(
                'Please make sure you have read and understood the LICENSE!'
        )
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'com.android.volley:volley:1.2.1'
}
